apply plugin: 'java'
apply plugin: 'maven'

ext {
    junitVersion = '4.8.2'
    artifactsDir = "$projectDir/artifacts"
    artifactGroupId = 'au.com.ausregistry'
}

sourceCompatibility = 1.6
version = toolkitVersion

repositories {
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:$junitVersion"
}

task cleanArtifacts(type: Delete) {
    delete artifactsDir
}

test {
    outputs.upToDateWhen { false }
    exclude '**/PerfTest.class'
    exclude '**/SessionTest.class'
    exclude '**/SessionManagerTest.class'
}

javadoc {
    destinationDir = file("$artifactsDir/javadoc")
}

clean.dependsOn cleanArtifacts

test.dependsOn jar

uploadArchives.dependsOn javadoc

uploadArchives {
    uploadDescriptor = false
    repositories {
        flatDir {
            dirs artifactsDir
        }
        if (System.properties.keySet().containsAll('repoUrl', 'repoUsername', 'repoPassword')) {
            mavenDeployer {
                repository(url: System.properties['repoUrl']) {
                    authentication(userName: System.properties['repoUsername'], password: System.properties['repoPassword'])
                }
                pom.groupId = artifactGroupId
                pom.artifactId = rootProject.name
                pom.version = toolkitVersion
            }
        }
    }
}